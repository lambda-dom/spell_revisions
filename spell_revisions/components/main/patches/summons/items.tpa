//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/blocks.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/components.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/installers.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/patchers.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/spells.tpa"


//Load references.
LAF get_component_resources_dir RET resources = dir END
LAF get_table_references STR_VAR
    tables = "spells"
    dir = "%resources%/2da/summons"
RET_ARRAY spells = references END

LAF get_table_references STR_VAR
    tables = "subspells"
    dir = "%resources%/2da/summons"
RET_ARRAY subspells = references END

LAF get_table_references STR_VAR
    tables = "5"
    dir = "%resources%/2da/divine"
RET_ARRAY divine_lvl5_spells = references END


//Patches.
DEFINE_PATCH_FUNCTION bat_immunities STR_VAR destination = "*" BEGIN
    //Patch bat swarm.
    LPF get_table_ref STR_VAR
        value = "bat_swarm"
        array = "spells"
        ext = "spl"
    RET res = resource END

    LPF ALTER_EFFECT INT_VAR
        check_globals = 1
        check_headers = 0
        verbose = 1
        match_opcode = 232                  //Cast spell on condition.
    STR_VAR
        match_resource = "swrm#res"
        resource = "%res%"
    END

    //Patch immunities.
    PATCH_FOR_EACH spell IN
        //Flying immunity.
        "WIZARD_GREASE"
        "CLERIC_ENTANGLE"
        
        //Blind immunity.
        "WIZARD_OBSCURING_MIST"
        "CLERIC_OBSCURING_MIST"
    BEGIN
        LPF get_spell_res STR_VAR
            spell = "%spell%"
        RET res = resource END

        //Sanitize.
        PATCH_IF ("%res%" STRING_EQUAL_CASE "*") BEGIN
            PATCH_FAIL "bat_immunities: spell '%spell%' does not exist."
        END

        LPF ADD_ITEM_EQEFFECT INT_VAR
            opcode = 206                    //Protection from spell
            parameter1 = 0                  //Zero out.
            parameter2 = 0
            target = 1                      //Self.
            timing = 2                      //Equipped.
            insert_point = 0 - 1            //Last.
        STR_VAR
            resource = "%res%"
        END
    END
END

DEFINE_PATCH_FUNCTION war_dog_scent STR_VAR destination = "*" BEGIN
    //Get reference.
    LPF get_table_ref STR_VAR
        value = "war_dog_scent"
        array = "spells"
        ext = "spl"
    RET res = resource END

    LPF ALTER_EFFECT INT_VAR
        check_globals = 1
        check_headers = 0
        verbose = 1
        match_opcode = 232                  //Cast spell on condition.
    STR_VAR
        match_resource = "spl#res"
        resource = "%res%"
    END
END

DEFINE_PATCH_FUNCTION undead_immunities STR_VAR destination = "*" BEGIN
    PATCH_FOR_EACH block IN
        "fear"
        "fatigue"
        "charm"
        "confusion"
        "disease"
        "paralysis"
        "poison"
        "sleep"
        "stun"
        "instant_death"
    BEGIN
        LPF patch_block INT_VAR
            match_opcode = 23               //Morale bonus.
            globals = 1
        STR_VAR
            block = "%block%"
        END
    END
END

DEFINE_PATCH_FUNCTION wolf_pack_attack BEGIN
    //Get reference.
    LPF get_table_ref STR_VAR
        value = "pack_attack"
        array = "spells"
        ext = "spl"
    RET res = resource END

    LPF ALTER_EFFECT INT_VAR
        check_globals = 1
        check_headers = 0
        verbose = 1
        match_opcode = 232              //Cast spell on condition.
    STR_VAR
        match_resource = "item#res"
        resource = "%res%"
    END
END

DEFINE_PATCH_FUNCTION giant_snake_immunities STR_VAR destination = "*" BEGIN
    PATCH_FOR_EACH block IN
        "grease"
        "entangle"
        "web"
        "poison"
    BEGIN
        LPF append_item_block STR_VAR block = "%block%" END
    END
END

DEFINE_PATCH_FUNCTION green_slime_immunities STR_VAR destination = "*" BEGIN
    PATCH_FOR_EACH block IN
        "charm"
        "confusion"
        "disease"
        "paralysis"
        "poison"
        "sleep"
        "stun"
    BEGIN
        LPF append_item_block STR_VAR block = "%block%" END
    END
END

DEFINE_PATCH_FUNCTION giant_spider_sting STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 146                  //Cast spell.
    STR_VAR
        resource = "spider_sting_poison"
        match_resource = "posn#res"
        references = "subspells"
    END
END

DEFINE_PATCH_FUNCTION sword_spider_sting STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 146                  //Cast spell.
    STR_VAR
        resource = "spider_sting_poison"
        match_resource = "posn#res"
        references = "subspells"
    END
END

DEFINE_PATCH_FUNCTION spider_immunities STR_VAR destination = "*" BEGIN
    PATCH_FOR_EACH block IN
        "web"
        "poison"
    BEGIN
        LPF append_item_block STR_VAR block = "%block%" END
    END
END

DEFINE_PATCH_FUNCTION shadow_touch STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 146                  //Cast spell.
    STR_VAR
        resource = "shadow_drain_strength"
        match_resource = "sdrn#res"
        references = "subspells"
    END
END

DEFINE_PATCH_FUNCTION wraith_touch STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 146                  //Cast spell.
    STR_VAR
        resource = "wraith_level_drain"
        match_resource = "ldrn#res"
        references = "subspells"
    END
END

DEFINE_PATCH_FUNCTION elemental_immunities STR_VAR destination = "*" BEGIN
    PATCH_FOR_EACH block IN
        "paralysis"
        "poison"
        "polymorph"
        "sleep"
        "stun"
    BEGIN
        LPF append_item_block STR_VAR
            block = "%block%"
        END
    END
END

DEFINE_PATCH_FUNCTION stalker_immunities STR_VAR destination = "*" BEGIN
    PATCH_FOR_EACH block IN
        "charm"
        "confusion"
        "paralysis"
        "poison"
        "sleep"
        "stun"
    BEGIN
        LPF append_item_block STR_VAR
            block = "%block%"
        END
    END
END

DEFINE_PATCH_FUNCTION wyvern_immunities STR_VAR destination = "*" BEGIN
    PATCH_FOR_EACH block IN
        "entangle"
        "grease"
        "web"
        "paralysis"
        "slow"
        "sleep"
        "stun"
    BEGIN
        LPF append_item_block STR_VAR
            block = "%block%"
        END
    END
END

DEFINE_PATCH_FUNCTION nishruu_immunities STR_VAR destination = "*" BEGIN
    PATCH_FOR_EACH block IN
        "poison"
    BEGIN
        LPF append_item_block STR_VAR
            block = "%block%"
        END
    END

    PATCH_FOR_EACH spell IN
        "WIZARD_LOWER_RESISTANCE"
        "WIZARD_PIERCE_MAGIC"
    BEGIN
        LPF get_spell_res STR_VAR
            spell = "%spell%"
        RET res = resource END

        //Sanitize.
        PATCH_IF ("%res%" STRING_EQUAL_CASE "*") BEGIN
            PATCH_FAIL "nishruu_immunities: spell '%spell%' does not exist."
        END

        LPF ADD_ITEM_EQEFFECT INT_VAR
            opcode = 206                    //Protection from spell
            parameter1 = 0                  //Zero out.
            parameter2 = 0
            target = 1                      //Self.
            timing = 2                      //Equipped.
            insert_point = 0 - 1            //Last.
        STR_VAR
            resource = "%res%"
        END
    END
END

DEFINE_PATCH_FUNCTION shambling_mound_fists STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 146                  //Cast spell.
    STR_VAR
        resource = "shambling_mound_constrict"
        match_resource = "entg#res"
        references = "spells"
    END
END

DEFINE_PATCH_FUNCTION shambling_mound_immunities STR_VAR destination = "*" BEGIN
    PATCH_FOR_EACH block IN
        "charm"
        "paralysis"
        "poison"
        "polymorph"
        "sleep"
        "stun"
    BEGIN
        LPF append_item_block STR_VAR
            block = "%block%"
        END
    END
END

DEFINE_PATCH_FUNCTION otyugh_immunities STR_VAR destination = "*" BEGIN
    PATCH_FOR_EACH block IN
        "disease"
    BEGIN
        LPF append_item_block STR_VAR
            block = "%block%"
        END
    END
END

DEFINE_PATCH_FUNCTION mordenkainen_immunities STR_VAR destination = "*" BEGIN
    PATCH_FOR_EACH block IN
        "blindness"
        "charm"
        "confusion"
        "berserk"
        "fear"
        "feeblemind"
        "sleep"
        "deafness"
        "paralysis"
        "stun"
        "petrification"
        "polymorph"
        "disease"
        "poison"
    BEGIN
        LPF append_item_block STR_VAR
            block = "%block%"
        END
    END
END

DEFINE_PATCH_FUNCTION umber_hulk_immunities STR_VAR destination = "*" BEGIN
    PATCH_FOR_EACH block IN
        "confusion"
    BEGIN
        LPF append_item_block STR_VAR
            block = "%block%"
        END
    END
END

DEFINE_PATCH_FUNCTION demon_immunities STR_VAR destination = "*" BEGIN
    PATCH_FOR_EACH block IN
        "poison"
        "confusion"
        "stun"
        "fear"
    BEGIN
        LPF append_item_block STR_VAR
            block = "%block%"
        END
    END
END

DEFINE_PATCH_FUNCTION glabrezu_pincers STR_VAR destination = "*" BEGIN
    //Get reference:
    //Resource does not exist at this point in the install.
    LPF get_array_element STR_VAR
        array = "divine_lvl5_spells"
        key = "true_seeing_a"
    RET resource = value END

    //Sanitize.
    PATCH_IF ("%resource%" STRING_EQUAL_CASE "*") BEGIN
        PATCH_FAIL "glabrezu_pincers: reference 'true_seeing_a' not found among divine level 5 spells table."
    END

    LPF ALTER_EFFECT INT_VAR
        verbose = 1
        match_opcode = 232                  //Cast spell on condition.
    STR_VAR
        match_resource = "spl#res"
        resource = "%resource%"
    END
END
