//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/2da.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/references.tpa"


//Encoders for patches.
DEFINE_PATCH_FUNCTION encode_elemental_protection_entry STR_VAR value = "" RET return BEGIN
    LPF get_resource_ref STR_VAR ref = "%value%" RET return = resource END
    PATCH_IF ("%return%" STRING_EQUAL_CASE "*") BEGIN
        PATCH_FAIL "encode_elemental_protection_entry: invalid '%value%' spell."
    END

    PATCH_IF NOT (FILE_EXISTS_IN_GAME "%return%.spl") BEGIN
        PATCH_FAIL "encode_elemental_protection_entry: resource '%return%.spl' for '%value%' does not exist in game."
    END
END


//Patches.
DEFINE_PATCH_FUNCTION enchanted_weapon STR_VAR destination = "*" BEGIN
    PATCH_IF NOT (7 = (STRING_LENGTH "%destination%")) BEGIN
        PATCH_FAIL "enchanted_weapon: resource '%destination%' is not 7 characters long."
    END

    COUNT_2DA_COLS cols
    COUNT_2DA_ROWS cols row_count
    //Loop: generate resource name and patch it.
    //Relies on convention association of item -> 1 character code. This is equivalent to:
    // DEFINE_ASSOCIATIVE_ARRAY names BEGIN
    //     "Axe"               => "a"
    //     "Club"              => "b"
    //     "Flail"             => "c"
    //     "Mace"              => "d"
    //     "Morning_Star"      => "e"
    //     "Hammer"            => "f"
    //     "Dagger"            => "g"
    //     "Short_Sword"       => "h"
    //     "Long_Sword"        => "i"
    //     "Bastard_Sword"     => "j"
    //     "Two_Handed_Sword"  => "k"
    //     "Scimitar"          => "l"
    //     "Katana"            => "m"
    //     "Halberd"           => "n"
    //     "Quarterstaff"      => "o"
    //     "Spear"             => "p"
    //     "Short_Bow"         => "q"
    //     "Long_Bow"          => "r"
    //     "Light_Xbow"        => "s"
    //     "Heavy_Xbow"        => "t"
    //     "Sling"             => "u"
    //     "Arrow"             => "v"
    //     "Bolt"              => "w"
    //     "Bullet"            => "x"
    // END

    FOR (i = 0; i < row_count; ++i) BEGIN
        INNER_PATCH_SAVE res "%destination%_" BEGIN
            code = 97 + i
            WRITE_BYTE 7 code
        END

        SET_2DA_ENTRY i 1 cols "%res%"
    END
END

DEFINE_PATCH_FUNCTION protection_elemental_energy STR_VAR destination = "*" BEGIN
    LPF patch_table_column STR_VAR patcher = "encode_elemental_protection_entry" END
END
