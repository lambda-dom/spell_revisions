//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/components.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/installers.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/patchers.tpa"

//Mod libraries.
INCLUDE "%MOD_FOLDER%/lib/utils.tpa"


//Load references.
LAF get_table_references STR_VAR
    tables = "effects"
RET_ARRAY effects = references END

LAF get_table_references STR_VAR
    tables = "items"
RET_ARRAY items = references END

LAF get_table_references STR_VAR
    tables = "subspells"
RET_ARRAY subspells = references END

LAF get_component_resources_dir RET resources = dir END
LAF get_table_references STR_VAR
    tables = "1"
    dir = "%resources%/2da/arcane"
RET_ARRAY spells = references END


//Patches.
DEFINE_PATCH_FUNCTION armor STR_VAR destination = "*" BEGIN
    LPF clone_to_display_string INT_VAR
        tra_ref = 10101
        match_opcode = 142                  //Display icon.
    END
END

DEFINE_PATCH_FUNCTION charm_person STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 177                  //Use Eff.
    STR_VAR
        resource = "charm_humanoid_neutral"
        ext = "eff"
        match_resource = "chrm#res"
        references = "effects"
    END
END

DEFINE_PATCH_FUNCTION color_spray STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 146                  //Cast spell.
    STR_VAR
        resource = "color_spray_slow"
        match_resource = "slow#res"
        references = "subspells"
    END

    LPF patch_resource_reference INT_VAR
        match_opcode = 146                  //Cast spell.
    STR_VAR
        resource = "color_spray_blind"
        match_resource = "blnd#res"
        references = "subspells"
    END

    LPF patch_resource_reference INT_VAR
        match_opcode = 146                  //Cast spell.
    STR_VAR
        resource = "color_spray_confusion"
        match_resource = "conf#res"
        references = "subspells"
    END
END

DEFINE_PATCH_FUNCTION protection_petrification STR_VAR destination = "*" BEGIN
    LPF clone_to_display_string INT_VAR
        tra_ref = 10102
        match_opcode = 142                  //Display icon.
    END
END

DEFINE_PATCH_FUNCTION protection_evil STR_VAR destination = "*" BEGIN
    LPF clone_to_display_string INT_VAR
        tra_ref = 10103
        match_opcode = 142                  //Display icon.
    END
END

DEFINE_PATCH_FUNCTION shield STR_VAR destination = "*" BEGIN
    LPF clone_to_display_string INT_VAR
        tra_ref = 10104
        match_opcode = 142                  //Display icon.
    END
END

DEFINE_PATCH_FUNCTION shocking_grasp STR_VAR destination = "*" BEGIN
    //Add melee effects.
    LPF get_spell_header_count RET headers_count = count END
    FOR (i = 1; i <= headers_count ; ++i) BEGIN
        LPF patch_resource_reference INT_VAR
            match_opcode = 111              //Create Weapon.
            header = i - 1
        STR_VAR
            resource = "shocking_grasp_%i%"
            ext = "itm"
            match_resource = "sgrp#res"
            references = "items"
        END
    END
END

DEFINE_PATCH_FUNCTION sleep STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 146                  //Cast spell.
    STR_VAR
        resource = "sleep_a"
        match_resource = "spl#res"
        references = "subspells"
    END
END

DEFINE_PATCH_FUNCTION chill_touch STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 111                  //Create Weapon.
    STR_VAR
        resource = "chill_touch"
        ext = "itm"
        match_resource = "chll#res"
        references = "items"
    END
END

DEFINE_PATCH_FUNCTION chromatic_orb STR_VAR destination = "*" BEGIN
    PATCH_WITH_SCOPE BEGIN
        DEFINE_ASSOCIATIVE_ARRAY spell_orbs BEGIN
            "chromatic_orb_white"       => "whte#orb"
            "chromatic_orb_aquamarine"  => "aqua#orb"
            "chromatic_orb_red"         => "red#orb"
        END

        PHP_EACH spell_orbs AS orb => match BEGIN
            LPF patch_resource_reference INT_VAR
                match_opcode = 146          //Cast spell.
            STR_VAR
                resource = "%orb%"
                match_resource = "%match%"
                references = "spells"
            END
        END
    END

    PATCH_WITH_SCOPE BEGIN
        //Patch in subspells.
        DEFINE_ASSOCIATIVE_ARRAY subspell_orbs BEGIN
            "chromatic_orb_green"   => "gren#orb"
            "chromatic_orb_yellow"  => "yell#orb"
            "chromatic_orb_violet"  => "viol#orb"
            "chromatic_orb_blue"    => "blue#orb"
        END


        PHP_EACH subspell_orbs AS orb => match BEGIN
            LPF patch_resource_reference INT_VAR
                match_opcode = 146          //Cast spell.
            STR_VAR
                resource = "%orb%"
                match_resource = "%match%"
                references = "subspells"
            END
        END
    END
END

DEFINE_PATCH_FUNCTION larloch_minor_drain STR_VAR destination = "*" BEGIN
    //Patch-in string ref.
    LPF get_spell_header_count RET headers_count = count END
    FOR (i = 1; i <= headers_count; ++i) BEGIN
        LPF patch_resource_reference INT_VAR
            match_opcode = 146              //Cast spell.
            header = i - 1
        STR_VAR
            resource = "larloch_minor_drain_%i%"
            match_resource = "spl#res"
            references = "subspells"
        END
    END
END

DEFINE_PATCH_FUNCTION reflected_image STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 146                  //Cast spell.
    STR_VAR
        resource = "reflected_image_a"
        match_resource = "refl#res"
        references = "spells"
    END
END

DEFINE_PATCH_FUNCTION spook STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 146                  //Cast spell.
    STR_VAR
        resource = "spook_panic"
        match_resource = "fear#res"
        references = "subspells"
    END
END

DEFINE_PATCH_FUNCTION obscuring_mist STR_VAR destination = "*" BEGIN
    //Add protection from cleric obscuring mist.
    LPF get_spell_res STR_VAR
        spell = "CLERIC_OBSCURING_MIST"
    RET res = resource END

    LPF CLONE_EFFECT INT_VAR
        check_globals = 0
        check_headers = 1
        verbose = 1
        match_opcode = 142                  //Display icon
        opcode = 206                        //Protection from spell.
        parameter1 = 0                      //Clean.
        parameter2 = 0
    STR_VAR
        resource = "%res%"
        insert = "last"
    END
END

DEFINE_PATCH_FUNCTION monster_summoning_1 BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 177                  //Use Eff.
    STR_VAR
        resource = "summon_gibberling"
        ext = "eff"
        match_resource = "summ#res"
        references = "effects"
    END
END
