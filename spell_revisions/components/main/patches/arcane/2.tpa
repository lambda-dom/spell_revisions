//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/components.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/installers.tpa"

//Mod libraries.
INCLUDE "%MOD_FOLDER%/lib/utils.tpa"


//Load reference tables.
LAF get_component_resources_dir RET resources = dir END
LAF get_table_references STR_VAR
    tables = "2"
    dir = "%resources%/2da/arcane"
RET_ARRAY spells = references END


//Patches.
DEFINE_PATCH_FUNCTION blur BEGIN
    LPF clone_to_display_string INT_VAR
        tra_ref = 10201
        match_opcode = 65                   //Blur.
    END
END

DEFINE_PATCH_FUNCTION detect_invisibility BEGIN
    //Get reference.
    LPF get_table_ref STR_VAR
        value = "detect_invisibility_a"
        array = "spells"
        ext = "spl"
    RET res = resource END

    LPF ALTER_EFFECT INT_VAR
        verbose = 1
        match_opcode = 146                  //Cast spell.
    STR_VAR
        match_resource = "spl#res"
        resource = "%res%"
    END
END

DEFINE_PATCH_FUNCTION luck BEGIN
    LPF clone_to_display_string INT_VAR
        tra_ref = 10202
        match_opcode = 142                  //Display icon.
    END
END

DEFINE_PATCH_FUNCTION mirror_image BEGIN
    LPF clone_to_display_string INT_VAR
        tra_ref = 10203
        match_opcode = 159                  //Mirror image.
    END
END

DEFINE_PATCH_FUNCTION strength BEGIN
    LPF clone_to_display_string INT_VAR
        tra_ref = 10204
        match_opcode = 142                  //Display icon.
    END
END

