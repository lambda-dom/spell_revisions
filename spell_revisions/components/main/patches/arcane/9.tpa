//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/components.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/installers.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/patchers.tpa"


//Load reference tables.
LAF get_table_references STR_VAR
    tables = "effects"
RET_ARRAY effects = references END

LAF get_table_references STR_VAR
    tables = "vvcs"
RET_ARRAY vvcs = references END

LAF get_component_resources_dir RET resources = dir END
LAF get_table_references STR_VAR
    tables = "9"
    dir = "%resources%/2da/arcane"
RET_ARRAY spells = references END


//Patches.
DEFINE_PATCH_FUNCTION spell_trap_b STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 321                  //Remove effects by resource.
    STR_VAR
        resource = "spell_trap"
        match_resource = "spl#res"
        references = "spells"
    END
END

DEFINE_PATCH_FUNCTION spell_trap STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 201                  //Spell deflection.
    STR_VAR
        resource = "spell_trap_b"
        match_resource = "spl#res"
        references = "spells"
    END
END

DEFINE_PATCH_FUNCTION meteor_swarm STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 215                  //Play visual effect.
    STR_VAR
        resource = "meteor_swarm"
        ext = "vvc"
        match_resource = "vvc#res"
        references = "vvcs"
    END
END

DEFINE_PATCH_FUNCTION bigbys_crushing_hand STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 146                  //Cast spell.
    STR_VAR
        resource = "bigbys_crushing_hand_a"
        match_resource = "spl#res"
        references = "spells"
    END
END
