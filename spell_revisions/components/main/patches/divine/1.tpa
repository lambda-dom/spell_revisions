//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/components.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/installers.tpa"

//Mod libraries.
INCLUDE "%MOD_FOLDER%/lib/utils.tpa"


//Load reference tables.
LAF get_component_resources_dir RET resources = dir END
LAF get_table_references STR_VAR
    tables = "items"
RET_ARRAY items = references END

LAF get_table_references STR_VAR
    tables = "subspells"
RET_ARRAY subspells = references END


//Patches.
DEFINE_PATCH_FUNCTION bless STR_VAR destination = "*" BEGIN
    LPF clone_to_display_string INT_VAR
        tra_ref = 10101
        match_opcode = 142                  //Display portrait icon.
    END
END

DEFINE_PATCH_FUNCTION cure_light_wounds STR_VAR destination = "*" BEGIN
    LPF clone_to_display_string INT_VAR
        tra_ref = 10102
        match_opcode = 240                  //Remove portrait icon.
    END
END

DEFINE_PATCH_FUNCTION entangle STR_VAR destination = "*" BEGIN
    //Get reference.
    LPF get_table_ref STR_VAR
        value = "entangle_a"
        array = "subspells"
        ext = "spl"
    RET res = resource END

    LPF ALTER_EFFECT INT_VAR
        verbose = 1
        match_opcode = 146                  //Cast spell.
    STR_VAR
        match_resource = "entg#res"
        resource = "%res%"
    END
END

DEFINE_PATCH_FUNCTION magical_stone STR_VAR destination = "*" BEGIN
    //Get reference.
    LPF get_table_ref STR_VAR
        value = "magical_stone"
        array = "items"
        ext = "itm"
    RET res = resource END

    LPF ALTER_EFFECT INT_VAR
        verbose = 1
        match_opcode = 111                  //Create Weapon.
    STR_VAR
        match_resource = "item#res"
        resource = "%res%"
    END
END

DEFINE_PATCH_FUNCTION protection_from_evil STR_VAR destination = "*" BEGIN
    LPF clone_to_display_string INT_VAR
        tra_ref = 10103
        match_opcode = 142                  //Display portrait icon.
    END
END

DEFINE_PATCH_FUNCTION shillelagh STR_VAR destination = "*" BEGIN
    LPF get_spell_header_count RET headers_count = count END
    FOR (i = 1; i <= headers_count; ++i) BEGIN
        //Get reference.
        LPF get_table_ref STR_VAR
            value = "shillelagh_%i%"
            array = "items"
            ext = "itm"
        RET res = resource END

        LPF ALTER_EFFECT INT_VAR
            verbose = 1
            header = i - 1
            match_opcode = 111              //Create Weapon.
        STR_VAR
            match_resource = "item#res"
            resource = "%res%"
        END
    END
END

DEFINE_PATCH_FUNCTION strength_stone BEGIN
    //Patch strings.
    LPF clone_to_display_string INT_VAR
        tra_ref = 10104
        match_opcode = 142                  //Display portrait icon.
    END

    //Patch in spell immunities.
    PATCH_FOR_EACH spell IN
        "DRAGON_WING_BUFFET"
    BEGIN
        LPF get_spell_res STR_VAR
            spell = "%spell%"
        RET res = resource END

        //Sanitize.
        PATCH_IF ("%res%" STRING_EQUAL_CASE "*") BEGIN
            PATCH_FAIL "strength_stone: unknown spell '%spell%'."
        END

        LPF CLONE_EFFECT INT_VAR
            check_globals = 0
            verbose = 1
            match_opcode = 44               //Strength bonus.
            opcode = 206                    //Protection from spell.
            parameter1 = 0                  //Zero out.
            parameter2 = 0
        STR_VAR
            resource = "%res%"
            insert = "last"
        END
    END
END
