//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/components.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/installers.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/references.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/spells.tpa"

//Mod libraries.
INCLUDE "%MOD_FOLDER%/lib/utils.tpa"


//Load reference tables.
LAF get_table_references STR_VAR
    tables = "effects"
RET_ARRAY effects = references END

LAF get_table_references STR_VAR
    tables = "items"
RET_ARRAY items = references END

LAF get_table_references STR_VAR
    tables = "subspells"
RET_ARRAY subspells = references END

LAF get_component_resources_dir RET resources = dir END
LAF get_table_references STR_VAR
    tables = "1"
    dir = "%resources%/2da/divine"
RET_ARRAY spells = references END


//Patches.
DEFINE_PATCH_FUNCTION bless STR_VAR destination = "*" BEGIN
    LPF clone_to_display_string INT_VAR
        tra_ref = 10101
        match_opcode = 142                  //Display portrait icon.
    END
END

DEFINE_PATCH_FUNCTION cure_light_wounds STR_VAR destination = "*" BEGIN
    LPF clone_to_display_string INT_VAR
        tra_ref = 10102
        match_opcode = 240                  //Remove portrait icon.
    END
END

DEFINE_PATCH_FUNCTION entangle STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 146                  //Cast spell.
    STR_VAR
        resource = "entangle_a"
        match_resource = "entg#res"
        references = "subspells"
    END
END

DEFINE_PATCH_FUNCTION magical_stone STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 111                  //Create Weapon.
    STR_VAR
        resource = "magical_stone"
        ext = "itm"
        match_resource = "item#res"
        references = "items"
    END
END

DEFINE_PATCH_FUNCTION protection_from_evil STR_VAR destination = "*" BEGIN
    LPF clone_to_display_string INT_VAR
        tra_ref = 10103
        match_opcode = 142                  //Display portrait icon.
    END
END

DEFINE_PATCH_FUNCTION shillelagh STR_VAR destination = "*" BEGIN
    LPF get_spell_header_count RET headers_count = count END
    FOR (i = 1; i <= headers_count; ++i) BEGIN
        LPF patch_resource_reference INT_VAR
            match_opcode = 111              //Create Weapon.
            header = i - 1
        STR_VAR
            resource = "shillelagh_%i%"
            ext = "itm"
            match_resource = "item#res"
            references = "items"
        END
    END
END

DEFINE_PATCH_FUNCTION strength_stone STR_VAR destination = "*" BEGIN
    //Patch strings.
    LPF clone_to_display_string INT_VAR
        tra_ref = 10104
        match_opcode = 142                  //Display portrait icon.
    END

    //Patch in spell immunities.
    PATCH_FOR_EACH spell IN
        "DRAGON_WING_BUFFET"
    BEGIN
        LPF get_spell_res STR_VAR
            spell = "%spell%"
        RET res = resource END

        //Sanitize.
        PATCH_IF ("%res%" STRING_EQUAL_CASE "*") BEGIN
            PATCH_FAIL "strength_stone: unknown spell '%spell%'."
        END

        LPF CLONE_EFFECT INT_VAR
            check_globals = 0
            verbose = 1
            match_opcode = 44               //Strength bonus.
            opcode = 206                    //Protection from spell.
            parameter1 = 0                  //Zero out.
            parameter2 = 0
        STR_VAR
            resource = "%res%"
            insert = "last"
        END
    END
END

DEFINE_PATCH_FUNCTION sunscorch STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 146                  //Cast spell.
    STR_VAR
        resource = "sunscorch_blind"
        match_resource = "blnd#res"
        references = "subspells"
    END

    //Patch in effects.
    LPF get_spell_header_count RET headers_count = count END
    FOR (i = 1; i <= headers_count; ++i) BEGIN
        LPF patch_resource_reference INT_VAR
            match_opcode = 177              //Use Eff.
            header = i - 1
        STR_VAR
            resource = "sunscorch_%i%"
            ext = "eff"
            match_resource = "blnd#res"
            references = "effects"
        END
    END
END

DEFINE_PATCH_FUNCTION regenerate_light_wounds STR_VAR destination = "*" BEGIN
    LPF clone_to_display_string INT_VAR
        tra_ref = 10105
        clone = 142                         //Display portrait icon.
    END
END

DEFINE_PATCH_FUNCTION cause_light_wounds BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 111                  //Create Weapon.
    STR_VAR
        resource = "sunscorch_blind"
        ext = "itm"
        match_resource = "item#res"
        references = "items"
    END
END

DEFINE_PATCH_FUNCTION faerie_fire BEGIN
    //Get reference.
    LPF get_table_ref STR_VAR
        value = "faerie_fire_a"
        array = "spells"
        ext = "spl"
    RET res = resource END

    LPF ALTER_EFFECT INT_VAR
        verbose = 1
        match_opcode = 146                  //Cast spell.
    STR_VAR
        match_resource = "ffir#res"
        resource = "%res%"
    END

    LPF ALTER_EFFECT INT_VAR
        verbose = 1
        match_opcode = 206                  //Protection from spell.
    STR_VAR
        match_resource = "ffir#res"
        resource = "%res%"
    END

    //Patch-in protection from aux glitterdust.
    //Spell not installed at this point, so use computed ref.
    LPF get_resource_ref STR_VAR
        ref = "#suf:a:WIZARD_GLITTERDUST"
    RET res = resource END

    LPF ALTER_EFFECT INT_VAR
        verbose = 1
        match_opcode = 206                  //Protection from spell.
    STR_VAR
        match_resource = "gdst#res"
        resource = "%res%"
    END
END

DEFINE_PATCH_FUNCTION obscuring_mist BEGIN
    //Add protection from cleric obscuring mist.
    LPF get_spell_res STR_VAR
        spell = "WIZARD_OBSCURING_MIST"
    RET res = resource END

    LPF CLONE_EFFECT INT_VAR
        check_globals = 0
        check_headers = 1
        verbose = 1
        match_opcode = 142                  //Display icon
        opcode = 206                        //Protection from spell.
        parameter1 = 0                      //Clean.
        parameter2 = 0
    STR_VAR
        resource = "%res%"
        insert = "last"
    END
END

DEFINE_PATCH_FUNCTION animal_summoning_1 BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 177                  //Use Eff.
    STR_VAR
        resource = "sunscorch_blind"
        ext = "eff"
        match_resource = "summ#res"
        references = "effects"
    END
END

DEFINE_PATCH_FUNCTION goodberry BEGIN
    LPF get_spell_header_count RET headers_count = count END
    FOR (i = 1; i <= headers_count; ++i) BEGIN
        LPF patch_resource_reference INT_VAR
            match_opcode = 122              //Create inventory item.
            header = i - 1
        STR_VAR
            resource = "goodberry_%i%"
            ext = "itm"
            match_resource = ""
            references = "items"
        END
    END
END
