//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/components.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/installers.tpa"
INCLUDE "%WEIDU_LIBRARY_DIR%/patchers.tpa"


//Load references.
LAF get_table_references STR_VAR
    tables = "effects"
RET_ARRAY effects = references END

LAF get_component_resources_dir RET resources = dir END
LAF get_table_references STR_VAR
    tables = "7"
    dir = "%resources%/2da/divine"
RET_ARRAY spells = references END

LAF get_table_references STR_VAR
    tables = "3"
    dir = "%resources%/2da/arcane"
RET_ARRAY wizard_level3_spells = references END


//Patches.
DEFINE_PATCH_FUNCTION summon_shambling_mound STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 177                  //Use Eff.
    STR_VAR
        resource = "summon_shambling_mound"
        ext = "eff"
        match_resource = "summ#res"
        references = "effects"
    END
END

DEFINE_PATCH_FUNCTION fire_storm_a STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 206                  //Protection from spell.
    STR_VAR
        //No cycle because resource must already exist.
        resource = "fire_storm"
        match_resource = "spl#res"
        references = "spells"
    END
END

DEFINE_PATCH_FUNCTION fire_storm_b STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 146                  //Cast spell.
    STR_VAR
        resource = "fire_storm_a"
        match_resource = "spl#res"
        references = "spells"
    END
END

DEFINE_PATCH_FUNCTION fire_storm STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 146                  //Cast spell.
    STR_VAR
        resource = "fire_storm_b"
        match_resource = "spl#res"
        references = "spells"
    END
END

DEFINE_PATCH_FUNCTION sunray STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 146                  //Cast spell.
    STR_VAR
        resource = "sunray_blind"
        match_resource = "blnd#res"
        references = "subspells"
    END

    LPF patch_resource_reference INT_VAR
        match_opcode = 146                  //Cast spell.
    STR_VAR
        resource = "sunray_b"
        match_resource = "udmg#res"
        references = "spells"
    END

    LPF patch_resource_reference INT_VAR
        match_opcode = 177                  //Use Eff.
    STR_VAR
        resource = "sunray_damage_death"
        ext = "eff"
        match_resource = "udth#res"
        references = "effects"
    END
END

DEFINE_PATCH_FUNCTION greater_restoration STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 146                  //Cast spell.
    STR_VAR
        resource = "haste_winded"
        match_resource = "wind#res"
        references = "wizard_level3_spells"
    END
END

DEFINE_PATCH_FUNCTION unholy_word STR_VAR destination = "*" BEGIN
    PATCH_WARN "unholy_word: effects not in yet."
END

DEFINE_PATCH_FUNCTION animal_summoning_7 STR_VAR destination = "*" BEGIN
    LPF patch_resource_reference INT_VAR
        match_opcode = 177                  //Use Eff.
    STR_VAR
        resource = "summon_polar_bear"
        ext = "eff"
        match_resource = "summ#res"
        references = "effects"
    END
END

