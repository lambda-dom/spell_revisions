IF
    Die()
THEN
    RESPONSE #100
        ReallyForceSpellDead(Myself,RED_HOLY_MIGHT)
        DestroySelf()
END

IF
    HasItem("dw#sumfi",Myself)
THEN
    RESPONSE #100
        DestroyItem("dw#sumfi")
        Continue()
END

IF
    Allegiance(LastSummonerOf(Myself),ENEMY)
    !Allegiance(Myself,ENEMY)
THEN
    RESPONSE #100
        SetGlobal("PitFiendControl","LOCALS",1)
        Enemy()
END

IF
    Global("PitFiendControl","LOCALS",0)
    OR(6)
        CheckStatGT(LastSummonerOf(Myself),18,INT)
        CheckStatGT(LastSummonerOf(Myself),18,WIS)
        CheckStatGT(LastSummonerOf(Myself),18,CHR)
        CheckStatGT(LastSummonerOf(Myself),20,LEVEL)
        CheckStatGT(LastSummonerOf(Myself),20,LEVEL2)
        CheckStatGT(LastSummonerOf(Myself),20,LEVEL3)
THEN
    RESPONSE #100
        FaceObject(LastSummonerOf(Myself))
        Wait(2)
        LeaveParty()
        ChangeEnemyAlly(Myself,ALLY)
        SetGlobal("PitFiendControl","LOCALS",1)
END

IF
    Global("PitFiendControl","LOCALS",0)
THEN
    RESPONSE #15
        FaceObject(LastSummonerOf(Myself))
        Wait(2)
        DisplayStringHead(Myself,12663)  // Now I will kill you all, foolish ones!
        Enemy()
        SetGlobal("PitFiendControl","LOCALS",1)
    RESPONSE #85
        FaceObject(LastSummonerOf(Myself))
        Wait(2)
        DisplayStringHead(Myself,7841)  // Very well, I shall follow you.
        LeaveParty()
        ChangeEnemyAlly(Myself,ALLY)
        SetGlobal("PitFiendControl","LOCALS",1)
END

IF
    AttackedBy([PC],DEFAULT)
THEN
    RESPONSE #100
        Enemy()
END

IF
    OR(3)
        See([0.0.DEMONIC.0.0.0.CHAOTIC_EVIL])
        See([0.0.0.TANARI.0.0.CHAOTIC_EVIL])
        See([0.0.0.0.0.SUMMONED_DEMON.CHAOTIC_EVIL])
THEN
    RESPONSE #100
        AttackOneRound(LastSeenBy(Myself))
END

IF
    !GlobalTimerNotExpired("castspell","LOCALS")
    See(NearestEnemyOf(Myself))
    !GlobalTimerNotExpired("fearaura","LOCALS")
THEN
    RESPONSE #100
        SetGlobalTimer("castspell","LOCALS",ONE_ROUND)
        ReallyForceSpell(Myself,DEMON_FEAR)
        SetGlobalTimer("fearaura","LOCALS",THREE_ROUNDS)
        Continue()
END

IF
    !GlobalTimerNotExpired("castspell","LOCALS")
    !AreaCheck("ar3008")
    !AreaCheck("ar3004")
    !GlobalTimerNotExpired("ImpInvisibility","LOCALS")
    !CheckStatGT(NearestEnemyOf(Myself),0,TRUE_SIGHT)
    !CheckStatGT(SecondNearestEnemyOf(Myself),0,TRUE_SIGHT)
    !CheckStatGT(ThirdNearestEnemyOf(Myself),0,TRUE_SIGHT)
    !CheckStatGT(FourthNearestEnemyOf(Myself),0,TRUE_SIGHT)
    !CheckStatGT(FifthNearestEnemyOf(Myself),0,TRUE_SIGHT)
    !CheckStatGT(SixthNearestEnemyOf(Myself),0,TRUE_SIGHT)
    !CheckStatGT(SeventhNearestEnemyOf(Myself),0,TRUE_SIGHT)
    !CheckStatGT(EighthNearestEnemyOf(Myself),0,TRUE_SIGHT)
    !CheckStatGT(NinthNearestEnemyOf(Myself),0,TRUE_SIGHT)
    !CheckStatGT(TenthNearestEnemyOf(Myself),0,TRUE_SIGHT)
    !StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
THEN
    RESPONSE #100
        SetGlobalTimer("castspell","LOCALS",ONE_ROUND)
        SpellNoDec(Myself,WIZARD_IMPROVED_INVISIBILITY)
        SetGlobalTimer("ImpInvisibility","LOCALS",FIVE_ROUNDS)
    RESPONSE #100
        Continue()
END

IF
    !GlobalTimerNotExpired("castspell","LOCALS")
    !AreaCheck("ar3004")
    !AreaCheck("ar3008")
    !GlobalTimerNotExpired("RemoveMagic","LOCALS")
    See(NearestEnemyOf(Myself))
    CheckStatLT(LastSeenBy(Myself),21,LEVEL)
    CheckStatLT(LastSeenBy(Myself),21,LEVEL2)
    CheckStatLT(LastSeenBy(Myself),21,LEVEL3)
    !StateCheck(LastSeenBy(Myself),STATE_DISABLED)
    OR(12)
        CheckStatGT(LastSeenBy(Myself),0,IMPROVEDHASTE)
        StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
        CheckStatGT(LastSeenBy(Myself),0,TRUE_SIGHT)
        CheckStatGT(LastSeenBy(Myself),0,STONESKINS)
        CheckStatGT(LastSeenBy(Myself),0,WIZARD_RESIST_FEAR)
        CheckStatGT(LastSeenBy(Myself),0,CLERIC_DEFENSIVE_HARMONY)
        CheckStatGT(LastSeenBy(Myself),0,CLERIC_CHAOTIC_COMMANDS)
        CheckStatGT(LastSeenBy(Myself),0,CLERIC_FREE_ACTION)
        CheckStatGT(LastSeenBy(Myself),0,WIZARD_FIRE_SHIELD)
        CheckStatGT(LastSeenBy(Myself),0,DEFENSIVE_MODIFIER)
        CheckStatGT(LastSeenBy(Myself),0,STRENGTH_MODIFIER)
        CheckStatGT(LastSeenBy(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
THEN
    RESPONSE #100
        SetGlobalTimer("castspell","LOCALS",ONE_ROUND)
        SpellNoDec(LastSeenBy(Myself),WIZARD_REMOVE_MAGIC)
        SetGlobalTimer("RemoveMagic","LOCALS",FIVE_ROUNDS)
    RESPONSE #100
        Continue()
END

IF
    !GlobalTimerNotExpired("castspell","LOCALS")
    !AreaCheck("ar3004")
    !AreaCheck("ar3008")
    !GlobalTimerNotExpired("MeteorSwarm","LOCALS")
    NumCreatureGT(NearestEnemyOf(Myself),3)
    See(NearestEnemyOf(Myself))
    CheckStatLT(LastSeenBy(Myself),75,RESISTMAGIC)
    CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
THEN
    RESPONSE #100
        SetGlobalTimer("castspell","LOCALS",ONE_ROUND)
        SpellNoDec(NearestEnemyOf(Myself),WIZARD_METEOR_SWARM)
        SetGlobalTimer("MeteorSwarm","LOCALS",EIGHT_HOURS)
    RESPONSE #100
        Continue()
END

IF
    !GlobalTimerNotExpired("castspell","LOCALS")
    !AreaCheck("ar3004")
    !AreaCheck("ar3008")
    !GlobalTimerNotExpired("Fireball","LOCALS")
    See(NearestEnemyOf(Myself))
    CheckStatLT(LastSeenBy(Myself),75,RESISTMAGIC)
    CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
    !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
THEN
    RESPONSE #100
        SetGlobalTimer("castspell","LOCALS",ONE_ROUND)
        SpellNoDec(NearestEnemyOf(Myself),WIZARD_FIREBALL)
        SetGlobalTimer("Fireball","LOCALS",FIVE_ROUNDS)
    RESPONSE #100
        Continue()
END

IF
    !GlobalTimerNotExpired("castspell","LOCALS")
    !AreaCheck("ar3008")
    !AreaCheck("ar3004")
    !GlobalTimerNotExpired("UnholyWord","LOCALS")
    NumCreatureGT(NearestEnemyOf(Myself),3)
    See(NearestEnemyOf(Myself))
    Alignment(LastSeenBy(Myself),MASK_GOOD)
    Range(LastSeenBy(Myself),10)
THEN
    RESPONSE #100
        SetGlobalTimer("castspell","LOCALS",ONE_ROUND)
        SpellNoDec(Myself,CLERIC_UNHOLY_WORD)
        SetGlobalTimer("UnholyWord","LOCALS",1000)
    RESPONSE #100
        Continue()
END

IF
    !GlobalTimerNotExpired("castspell","LOCALS")
    !AreaCheck("ar3004")
    !AreaCheck("ar3008")
    !GlobalTimerNotExpired("SymbolWeakness","LOCALS")
    See(NearestEnemyOf(Myself))
    !General(LastSeenBy(Myself),UNDEAD)
    CheckStatLT(LastSeenBy(Myself),75,RESISTMAGIC)
    !StateCheck(LastSeenBy(Myself),STATE_DISABLED)
THEN
    RESPONSE #100
        SetGlobalTimer("castspell","LOCALS",ONE_ROUND)
        SpellNoDec(LastSeenBy(Myself),WIZARD_NPC_SYMBOL_FEAR)
        SetGlobalTimer("SymbolWeakness","LOCALS",FIVE_ROUNDS)
    RESPONSE #100
        Continue()
END

IF
    See(NearestEnemyOf(Myself))
THEN
    RESPONSE #100
        AttackReevaluate(LastSeenBy(Myself),30)
END

IF
    ActionListEmpty()
    Allegiance(Myself,ENEMY)
    !Detect(NearestEnemyOf(Myself))
    !AreaCheck("ar3004")
    InMyArea(Player1)
    InMyArea(Player2)
    InMyArea(Player3)
    InMyArea(Player4)
    InMyArea(Player5)
    InMyArea(Player6)
THEN
    RESPONSE #100
        ReallyForceSpellRES(INNATE_TELEPORT_WITHOUT_ERROR,Player1)
    RESPONSE #100
        ReallyForceSpellRES(INNATE_TELEPORT_WITHOUT_ERROR,Player2)
    RESPONSE #100
        ReallyForceSpellRES(INNATE_TELEPORT_WITHOUT_ERROR,Player3)
    RESPONSE #100
        ReallyForceSpellRES(INNATE_TELEPORT_WITHOUT_ERROR,Player4)
    RESPONSE #100
        ReallyForceSpellRES(INNATE_TELEPORT_WITHOUT_ERROR,Player5)
    RESPONSE #100
        ReallyForceSpellRES(INNATE_TELEPORT_WITHOUT_ERROR,Player6)
END

IF
    ActionListEmpty()
    Allegiance(Myself,ENEMY)
    !Detect(NearestEnemyOf(Myself))
    !AreaCheck("ar3004")
    InMyArea(Player1)
    InMyArea(Player2)
    InMyArea(Player3)
THEN
    RESPONSE #100
        ReallyForceSpellRES(INNATE_TELEPORT_WITHOUT_ERROR,Player1)
    RESPONSE #100
        ReallyForceSpellRES(INNATE_TELEPORT_WITHOUT_ERROR,Player2)
    RESPONSE #100
        ReallyForceSpellRES(INNATE_TELEPORT_WITHOUT_ERROR,Player3)
END

IF
    ActionListEmpty()
    Allegiance(Myself,ENEMY)
    !Detect(NearestEnemyOf(Myself))
    !AreaCheck("ar3004")
    InMyArea(Player1)
THEN
    RESPONSE #100
        ReallyForceSpellRES(INNATE_TELEPORT_WITHOUT_ERROR,Player1)
END

IF
    !Allegiance(Myself,EVILCUTOFF)
    !See([EVILCUTOFF])
    ActionListEmpty()
    InParty(LastSummonerOf(Myself))
    !Range(LastSummonerOf(Myself),15)
    InMyArea(LastSummonerOf(Myself))
    InActiveArea(Myself)
    InActiveArea(LastSummonerOf(Myself))
THEN
    RESPONSE #100
        Formation(LastSummonerOf(Myself),[-4.-4])
END

IF
    !Allegiance(Myself,EVILCUTOFF)
    !See([EVILCUTOFF])
    ActionListEmpty()
    !InParty(LastSummonerOf(Myself))
    !Range(Player1,15)
    InMyArea(Player1)
    InActiveArea(Myself)
THEN
    RESPONSE #100
        Formation(Player1,[-4.-4])
END

