//Libraries.
INCLUDE "%WEIDU_LIBRARY_DIR%/subspells.tpa"


//Initialization.
OUTER_TEXT_SPRINT res_dir "%MOD_FOLDER%/components/main"
OUTER_TEXT_SPRINT tra_dir "%MOD_FOLDER%/languages/%LANGUAGE%/main"


//Patchers.
DEFINE_PATCH_FUNCTION clone_to_display_string INT_VAR
    check_headers = 1
    check_globals = 0
    header = 0 - 1
    match_opcode = 142
    tra_ref = 0
STR_VAR
    insert = "below"
BEGIN
    //Resolve reference.
    tlk_ref = RESOLVE_STR_REF ((AT tra_ref))

    //Insert opcode.
    LPF CLONE_EFFECT INT_VAR
        check_globals = check_globals
        check_headers = check_headers
        verbose = 1
        header = header
        match_opcode = match_opcode
        opcode = 139                        //Display string.
        parameter1 = tlk_ref
        parameter2 = 0                      //Clean.
        timing = 1                          //Instantaneous.
        duration = 0
        special = 0                         //Clean.
    STR_VAR
        resource = ""                       //Clean.
        insert = "%insert%"
    END
END

//Simplify calling install_subspells.
DEFINE_ACTION_FUNCTION install_subspells_mod STR_VAR table = "" BEGIN
    LAF install_subspells STR_VAR
        table = "%table%"
        subspells = "%res_dir%/resources/2da/subspells/master.2da"
        dir = "%res_dir%/resources/spl/subspells"
        tra = "%tra_dir%/subspells/spells.tra"
        patches = "%res_dir%/patches/subspells/patches.tpa"
    END
END
